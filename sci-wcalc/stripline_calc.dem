// -*- scilab -*-
// $Id: air_coil_calc.dem,v 1.2 2009/01/11 04:22:23 dan Exp $
//
// Copyright (c) 2009 Dan McMahill
// All rights reserved.
//
// This program is free software; you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation; version 2 of the License.
// 
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
// 
// You should have received a copy of the GNU General Public License
// along with this program; if not, write to the Free Software
// Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301  USA


// -----------------------------------------------
// Single Stripline Analysis
// -----------------------------------------------

sf   = 25.4e-6; // mils to meters conversion
w    = 50*sf;   // width of lines
h    = 62*sf;   // total dielectric thickness
l    = 1000*sf; // physical length of the lines
tmet = 0.7*sf;  // metal thickness
rho  = 1.7e-8;  // metal resistivity (Ohm-meters)
rough= 0.0;     // metal surface roughness
er   = 4.8;     // substrate relative permitivity
tand = 0.01;    // dielectric loss tangent
f    = 900e6;   // frequency of operation

[z0,elen,loss,L,R,C,G,lc,ld,deltal,depth] = ...
    stripline_calc(w,h,l,tmet,rho,rough,er,tand,f);
disp(sprintf('z0   = %8.4g Ohms',z0));
disp(sprintf('elen = %8.4g degrees',elen));
disp(sprintf('loss = %8.4g dB',loss));
disp(sprintf('L    = %8.4g H/m',L));
disp(sprintf('R    = %8.4g Ohms/m',R));
disp(sprintf('C    = %8.4g F/m',C));
disp(sprintf('G    = %8.4g S/m',G));
disp(sprintf('lc   = %8.4g dB/m',lc));
disp(sprintf('ld   = %8.4g dB/m',ld));
disp(sprintf('open end correction = %8.4g mil', deltal/sf));
disp(sprintf('skin depth = %8.4g um', depth*1e6));

// Sweep width from 0.01 to 100.0 times the
// dielectric thickness
wrel = logspace(-1,1,256)';
w = wrel*h; 
[z0,elen,loss,L,R,C,G,lc,ld,deltal,depth] = ...
    stripline_calc(w,h,l,tmet,rho,rough,er,tand,f);
xset("window", 0); xbasc(); xselect();
plot2d(wrel,z0, logflag="ln");
xgrid(3);
xtitle('Characteristic Impedance vs. Trace Width', ...
    'Width/Dielectric Thickness','Ohms');

// Sweep width from 0.01 to 100.0 times the
// dielectric thickness
wrel = logspace(-2,2,256)';
w = wrel*h; 
[z0,elen,loss,L,R,C,G,lc,ld,deltal,depth] = ...
    stripline_calc(w,h,l,tmet,rho,rough,er,tand,f);
xset("window", 1); xbasc(); xselect();
plot2d(wrel,z0, logflag="ll");
xgrid(3);
xtitle('Characteristic Impedance vs. Trace Width', ...
    'Width/Dielectric Thickness','Ohms');

