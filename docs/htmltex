# !/bin/awk -f
#
# $Id$
#

BEGIN{
    img=0;
    state=0;

# how much room to leave around the figures
    border=4;

# resolution (DPI) 
    sf=4;
  res=72;
  xres=sf*res;
  yres=sf*res;

  tmp=ENVIRON["TMP_DIR"];
  if (tmp == ""){
    tmp="/tmp";
  }
  printf("using temporary directory \"%s\"\n",tmp);

  tmpbase="htmltex";
  printf("using temporary basename \"%s\"\n",tmpbase);

  outf="air_coil_theory.html";
  texf=sprintf("%s/%s.tex",tmp,tmpbase);
}

state==2 {
  state=0;
}

/<\/LATEXEQ>/ {
  print "\\]" > texf;
  print "\\end{document}\n\n" > texf;
  close(texf);
  print "------ Processing ------";

# run latex on the file
  cmd=sprintf("cd %s && latex %s.tex",tmp,tmpbase);
  print cmd;
  do_system(cmd);

# run dvips on the .dvi file
  cmd=sprintf("cd %s && dvips %s.dvi -o %s.ps",tmp,tmpbase,tmpbase);
  print cmd;
  do_system(cmd);

# extract the bounding box of the ps file
#%%BoundingBox: 237 615 374 659
  cmd=sprintf("cd %s && " \
	      "gs -q -dNOPAUSE -sDEVICE=bbox -- %s.ps 2>&1",
	      tmp,tmpbase);
  print cmd;
  while((cmd|getline line)>0){
    if (line ~ /%%BoundingBox:/){
      split(line,bbox," ");
      width  = (bbox[4] - bbox[2])+2*border;
      height = (bbox[5] - bbox[3])+2*border;
      dx     = (-bbox[2]) + border;
      dy     = (-bbox[3]) + border;
      print "width = "width" height = "height;
      print "dx = "dx" dy = "dy;
    }
  }
  close(cmd);

# generate the postscript to png conversion command
  cmd=sprintf("echo %d %d translate |" \
	      "cat - %s/%s.ps | " \
	      "gs -sDEVICE='pnggray' -sOutputFile='file%d.png' " \
	      "-g'%dx%d' -r'%dx%d' -q -dNOPAUSE -", \
	      dx,dy,tmp,tmpbase,img,sf*width,sf*height,xres,yres);
  print cmd;
  do_system(cmd);

# clean up
  cmd=sprintf("cd %s && rm %s.*",tmp,tmpbase);
  print cmd;
  do_system(cmd);
  
  print "---- Done Processing ---";

  printf("<BR><CENTER>\n" \
	 "<IMG SRC=\"file%d.png\" \n" \
	 "     WIDTH=%d HEIGHT=%d \n" \
	 "     ALT=\"[eq %d]\" \n"\
	 "     >\n" \
	 "</CENTER><BR>\n",img,width,height,img) > outf;

# update the image number
  img++;

# return to state 0 (via 2) (waiting for <LATEXEQ>)
  state=2;
}

state==1 {
  print > texf;
}

/<LATEXEQ>/ {
    print "\\documentclass[12pt]{article}" > texf;
    print "\\usepackage{graphicx}" > texf;
    print "\\usepackage{latexsym}" > texf;
    print "\\oddsidemargin=0in" > texf;
    print "\\textwidth=6.5in" > texf;
    print "\\topmargin=0in" > texf;
    print "\\textheight=609pt" > texf;
    print "\\parskip=14pt" > texf;
    print "\\setlength{\\unitlength}{0.5cm}" > texf;
    print "\\pagestyle{empty}" > texf;
    print "\\begin{document}" > texf;
    print "\\LARGE" > texf;
    print "\\[" > texf;
    state=1;
}



function do_system(cmd,rv){
    rv = system(cmd);
    if (rv != 0){
        printf("Error:  system(\"%s\") returned %d\n",cmd,rv);
        exit(1);
    }
    return(0);
}


# for all other lines, just copy to output
state==0{
    print > outf;
}

END{
    close(outf);
}
